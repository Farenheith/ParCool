buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}
apply plugin: 'org.spongepowered.mixin'

compileJava.options.encoding("UTF-8")
compileTestJava.options.encoding("UTF-8")

mixin {
    add sourceSets.main, 'parcool.refmap.json'
    config 'parcool.mixins.json'
}

//Version Format (MajorMOD.MajorAPI.Minor.Patch)
var mod_version_code = "${major_mod}.${major_api}.${minor}.${patch}"
version = "${mc_version}-${mod_version_code}"
group = 'com.alrex'
base {
    archivesBaseName = 'ParCool'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: mc_version
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            mods {
                parcool {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            mods {
                parcool {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'parcool', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                parcool {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
//sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://maven.blamejared.com' }
    maven { url = 'https://www.cursemaven.com' }
}
dependencies {
    // Specify the version of Minecraft to use.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    //compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

    compileOnly fg.deobf("curse.maven:BetterThirdPerson-435044:4177088")
    compileOnly fg.deobf("curse.maven:ShoulderSurfing-243190:6387974")
    runtimeOnly fg.deobf("curse.maven:ShoulderSurfing-243190:6387974")

    compileOnly fg.deobf("curse.maven:feathers-699933:4573576")
    runtimeOnly fg.deobf("curse.maven:feathers-699933:4573576")

    compileOnly fg.deobf("curse.maven:epic-fight-mod-405076:${epicfight_version}")
    runtimeOnly fg.deobf("curse.maven:epic-fight-mod-405076:${epicfight_version}")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "ParCool!",
                "Specification-Vendor"    : "alRex",
                "Specification-Version" : "${mod_version_code}",
                "Implementation-Title"    : project.name,
                "Implementation-Version": "${mod_version_code}",
                "Implementation-Vendor"   : "alRex",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"            : "parcool.mixins.json"
        ])
    }
    finalizedBy('reobfJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}